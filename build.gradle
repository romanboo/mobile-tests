plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.11.2'
}
def selenideVersion = "6.19.0",
    restAssuredVersion = "5.3.1",
    allureVersion = "2.27.0"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

repositories {
    mavenCentral()
}

compileTestJava {
    options.encoding = 'UTF-8'
}

dependencies {
    testImplementation(
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "io.rest-assured:json-schema-validator:$restAssuredVersion",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            'org.junit.jupiter:junit-jupiter:5.10.2',
            'org.slf4j:slf4j-simple:2.0.13',
            'org.assertj:assertj-core:3.26.0',
            'org.aeonbits.owner:owner:1.0.12',
            "io.appium:java-client:8.5.0"
    )
    runtimeOnly 'org.aspectj:aspectjweaver:1.9.22.1'
}

tasks.withType(Test) {
    useJUnitPlatform()
    systemProperties(System.getProperties())

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task ios(type: Test) {
    useJUnitPlatform{
        includeTags("ios")
    }
}

task android(type: Test) {
    useJUnitPlatform{
        includeTags("android")
    }
}